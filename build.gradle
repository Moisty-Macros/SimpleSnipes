import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
        maven { url 'https://maven.minecraftforge.net/' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.github.Skytils:ForgeGradle:6f5327'
        classpath 'com.github.Skytils:mixingradle:d5f9873d60'
        classpath "com.github.johnrengelman:shadow:6.1.0"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'java'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url = "https://repo.sk1er.club/repository/maven-public" }
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
}

dependencies {
    implementation("gg.essential:loader-launchwrapper:1.1.2")
    implementation("gg.essential:essential-1.8.9-forge:1733") {
        exclude module: 'gson'
    }
    implementation('org.java-websocket:Java-WebSocket:1.5.2')

    implementation("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }

    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")
}

shadowJar {
    dependencies {
        include(dependency("gg.essential:loader-launchwrapper:1.1.2"))
        include(dependency("org.java-websocket:Java-WebSocket:1.5.2"))
        include(dependency("org.slf4j:slf4j-api:1.7.25"))
        include(dependency("org.spongepowered:mixin:0.7.11-SNAPSHOT"))
    }
    exclude('dummyThing')
    exclude('META-INF/maven/')
}

reobf {
    shadowJar {} // Reobfuscate the shadowed JAR
}

test {
    systemProperty "file.encoding", "utf-8"
}

version = "1.0"
group = "me.mindlessly.notenoughcoins"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    mappings = "stable_22"
    clientRunArgs += "--tweakClass org.spongepowered.asm.launch.MixinTweaker"
    clientRunArgs += "--mixin mixins.nec.json"
    // clientRunArgs += ["--tweakClass=gg.essential.loader.stage0.EssentialSetupTweaker"]
}

jar {
    manifest.attributes(
            'FMLCorePlugin': "${project.group}.tweaker.NotEnoughCoinsLoadingPlugin",
            'ForceLoadAsMod': true,
            'FMLCorePluginContainsFMLMod': true,
            'MixinConfigs': 'mixins.nec.json',
            'ModSide': 'CLIENT',
            'TweakClass': 'gg.essential.loader.stage0.EssentialSetupTweaker'
    )
}

sourceJar {
    enabled = false
}

mixin {
    add sourceSets.main, "mixins.nec.refmap.json"
}

// Automatic version replacements
task processSource(type: Sync) {
    from sourceSets.main.java
    inputs.property 'version', project.version
    filter(ReplaceTokens, tokens: [version: project.version])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
